@prefix sd:      <http://www.w3.org/ns/sparql-service-description#>.
@prefix dct:     <http://purl.org/dc/terms/>.
@prefix shacl:   <http://www.w3.org/ns/shacl#>.
@prefix hydra:   <http://www.w3.org/ns/hydra/core#>.
@prefix schema:  <http://schema.org/>.
@prefix skos:    <http://www.w3.org/2004/02/skos/core#>.
@prefix sms:     <http://ns.inria.fr/sparql-micro-service#>.

template st:oneSms {

    format { <../tpl2_oneSms.html> ?serviceName ?webApiCode ?service }
    
} where {
    # Retrieve the root URL from the query string parameter (e.g. ?param=http://example.org/service)
    BIND(st:get(st:param) as ?rootUrl)

    ?service
        a sd:Service;
        schema:name ?serviceName;
        dct:source ?source.
        
    ?source
        a schema:WebAPI;
        schema:name ?webApiName.
        
    OPTIONAL { ?source schema:url ?webApiUrl }

    BIND(
        if (bound(?webApiUrl), 
            concat( '<a href="', ?webApiUrl, '" target="_new">', ?webApiName, '</a>'),
            ?webApiName
        )
        as ?webApiCode
    )

    # Select only services with the given root url
    FILTER(strstarts(str(?service), str(?rootUrl)))
    
} ORDER BY ?webApiName

