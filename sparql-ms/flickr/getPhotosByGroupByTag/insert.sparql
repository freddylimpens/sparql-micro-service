INSERT {
    ?photoUri a schema:Photograph;
        foaf:depiction ?img;
        schema:author ?authorUrl;
        dce:creator ?authorName;
        dce:title ?title;
        .
}
WHERE {
    ?photo api:id ?id; api:secret ?secret; api:server ?server; api:farm ?farm;
        api:ispublic ?public;
        api:title ?title;
        api:owner ?owner; api:ownername ?authorName;
    .

    bind (IRI(concat("http://erebe-vm2.i3s.unice.fr/ld/flickr/photo/", ?id)) AS ?photoUri)
    bind (IRI(concat("https://flickr.com/photos/", ?owner)) AS ?authorUrl)
    bind (IRI(concat("https://farm", ?farm, ".staticflickr.com/", ?server, "/", ?id, "_", ?secret, "_z.jpg")) AS ?img)

    filter(?public = "1"^^xsd:integer)
}